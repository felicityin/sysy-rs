CompUnit      ::= [CompUnit] (Decl | FuncDef);

Decl          ::= ConstDecl | VarDecl;
ConstDecl     ::= "const" BType ConstDef {"," ConstDef} ";";
BType         ::= "int";
ConstDef      ::= IDENT {"[" ConstExp "]"} "=" ConstInitVal;
ConstInitVal  ::= ConstExp | "{" [ConstInitVal {"," ConstInitVal}] "}";
VarDecl       ::= BType VarDef {"," VarDef} ";";
VarDef        ::= IDENT {"[" ConstExp "]"}
                | IDENT {"[" ConstExp "]"} "=" InitVal;
InitVal       ::= Exp | "{" [InitVal {"," InitVal}] "}";

FuncDef       ::= FuncType IDENT "(" [FuncFParams] ")" Block;
FuncType      ::= "void" | "int";
FuncFParams   ::= FuncFParam {"," FuncFParam};
FuncFParam    ::= BType IDENT ["[" "]" {"[" ConstExp "]"}];

Block         ::= "{" {BlockItem} "}";
BlockItem     ::= Decl | Stmt;
Stmt          ::= LVal "=" Exp ";"
                | [Exp] ";"
                | Block
                | "if" "(" Exp ")" Stmt ["else" Stmt]
                | "while" "(" Exp ")" Stmt
                | "break" ";"
                | "continue" ";"
                | "return" [Exp] ";";

ConstExp      ::= Exp;
Exp           ::= LOrExp;
LOrExp        ::= LAndExp | LOrExp "||" LAndExp;
LAndExp       ::= EqExp | LAndExp "&&" EqExp;
EqExp         ::= RelExp | EqExp ("==" | "!=") RelExp;
RelExp        ::= AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp;
AddExp        ::= MulExp | AddExp ("+" | "-") MulExp;
MulExp        ::= UnaryExp | MulExp ("*" | "/" | "%") UnaryExp;
UnaryExp      ::= PrimaryExp | IDENT "(" [FuncRParams] ")" | UnaryOp UnaryExp;
FuncRParams   ::= Exp {"," Exp};
PrimaryExp    ::= "(" Exp ")" | LVal | Number;
LVal          ::= IDENT {"[" Exp "]"};
Number        ::= INT_CONST;
UnaryOp       ::= "+" | "-" | "!";
